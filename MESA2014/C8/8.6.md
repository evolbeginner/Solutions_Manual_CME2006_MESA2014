**Solution.**

I choose to use RevBayes (Hohna et al. 2016), the so-called new
generation of MrBayes (MrBayes is no longer updated). The following
takes JC69 as an example (see "JC69.revbayes"). More information can be
found at RevBayes' tutorial website at
<https://revbayes.github.io/tutorials/ctmc>. As to the alignment,
"sample.fasta" given in the folder "data/" of the current chapter is the
same as the one used in Problem 4.6 ("rbcL.nogaps_amb.fas").

```RevBayes
#RevBayes
# load alignment
data <- readDiscreteCharacterData("sample.fasta")
# taxon info
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3 
taxa <- data.taxa()
# moves
moves    = VectorMoves()
monitors = VectorMonitors()
# transition rate matrix
Q <- fnJC(4)
# topology prior
topology ~ dnUniformTopology(taxa)
# NNI+SPR
moves.append( mvNNI(topology, weight=num_taxa) )
moves.append( mvSPR(topology, weight=num_taxa/10.0) )
for (i in 1:num_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves.append( mvScale(br_lens[i]) )
}
TL := sum(br_lens)
# phylogeny
psi := treeAssembly(topology, br_lens)

# set up everything
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")
seq.clamp(data)
mymodel = model(Q)
monitors.append( mnModel(filename="output/JC69/sample_JC.log", printgen=10) )
monitors.append( mnFile(filename="output/JC69/sample_JC.trees", printgen=10, psi) )
monitors.append( mnScreen(printgen=100, TL) )
mymcmc = mcmc(mymodel, monitors, moves)

# run mcmc
mymcmc.burnin(1000,100)
mymcmc.run(generations=2000)
q()
```

We can further summarize the result by the following command where the
MAP tree is generated.

```RevBayes
#RevBayes
# summarize
treetrace = readTreeTrace("output/JC69/sample_JC.trees ", treetype="non-clock")
map_tree = mapTree(treetrace,"output/JC69/sample_JC_MAP.tree")
```

Now, open the file "output/JC69/sample_JC.log" in Tracer. You should see
something like the following.

<p><img src=img/8.6-1.png></p>

The MAP tree is displayed as follows.

<p><img src=img/8.6-2.png></p>

Another script, "GTR+G5.revbayes" is used for tree construction under
the model GTR+G5. Specifically, to enable the gamma rate variation in
RevBayes, it is needed to specify the following.

```revBayes
#RevBayes
alpha ~ dnUniform( 0.0, 10 )
sr := fnDiscretizeGamma( alpha, alpha, 5 ) # discrete gamma w 5 categories
moves.append( mvScale(alpha, weight=2.0) )
```

**Bonus question.**

Compare the estimated parameters by RevBayes with those by IQ-Tree or
raxml-ng in Problem 4.6 under the same model. What do you see?
